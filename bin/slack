#!/usr/bin/env coffee

Slack     = require '..'
Readline  = require 'readline'

token = process.argv[2]
if not token
  console.error "Token is required"
  process.exit 1
else
  shutdown = false
  slack = new Slack token
  rl = Readline.createInterface(process.stdin, process.stdout)

  slack.on 'error', (error) ->
    console.error "Error: %s", error
    process.exit 1

  slack.on 'open', ->
    console.log "Welcome to Slack. You are %s of %s", slack.self.name, slack.team.name
    channels = []
    for k of slack.channels
      channel = slack.channels[k]
      if channel.is_member then channels.push '#'+channel.name

    console.log "You are in: %s", channels.join(', ')

    groups = []
    for k of slack.groups
      group = slack.groups[k]
      if group.is_open and not group.is_archived then groups.push group.name

    if groups.length then console.log "As well as: %s", groups.join(', ')

    rl.setPrompt '> ', 2
    rl.prompt()

  slack.on 'close', ->
    console.warn 'Disconnected!'
    if not shutdown then setTimeout slack.login(), 5000

  slack.on 'message', (message) ->
    str = message.toString()
    if str then console.log str


  rl.on 'line', (line) ->
    cmd = line.split ' ', 1
    rest = line.replace cmd[0], ''
      .trim()

    switch cmd[0]
      when "/msg"
        if rest
          name = rest.split ' ', 1
          text = rest.replace name, ''
            .trim()

          channel = slack.getChannelGroupOrDMByName name[0]
          if not channel
            console.log "Could not find channel '%s'", name[0]
          else if not text
            console.log "Need something to send!"
          else
            console.log "Sending '%s' to '%s'", text, name[0]
            channel.send text
        else
          console.log "Sorry, what? Try /help"

      when "/quit"
        rl.close()
        return

      when "/help"
        console.log 'Commands:'
        console.log '/msg channel text'
        console.log '/quit'

      else
        console.log "Sorry, what? Try /help"

    rl.setPrompt '> ', 2
    rl.prompt()

  rl.on 'close', ->
    console.log 'Shutting down...'
    shutdown = true
    slack.disconnect()

  console.log 'Connecting...'
  slack.login()
